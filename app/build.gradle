apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply from: 'keys.gradle'

android {

    def versionFile = file('version.txt')
    def verCode = 1
    if (versionFile.canRead()) {
        def line = (new FileInputStream(versionFile)).readLines()[0]
        verCode = line.toInteger()
        List<String> runTasks = gradle.startParameter.getTaskNames()
        for (String item : runTasks)
            if (item.contains("publishReleaseApk") || item.contains("publish")) {
                verCode++
                versionFile.text = verCode.toString()
                break
            }
    } else {
        throw new GradleException("Could not read version.txt!")
    }

    kotlinOptions.useIR = true

    compileSdkVersion 31
    defaultConfig {
        applicationId "com.arn.scrobble"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode verCode
        versionName ((int)(versionCode/100) + "." + (versionCode%100) +  " - " + new Date().format('dd.MMM.YY'))
        setProperty("archivesBaseName", "pScrobbler")
        vectorDrawables.useSupportLibrary = true
//        resConfigs "af", "am", "ar", "as", "az", "be", "bg", "bn", "bs", "ca", "cs", "da", "de", "el", "en", "es", "et", "eu", "fa", "fi", "fil", "fr", "gl", "gsw", "gu", "he", "hi", "hr", "hu", "hy", "id", "is", "it", "iw", "ja", "ka", "kk", "km", "kn", "ko", "ky", "ln", "lo", "lt", "lv", "mk", "ml", "mn", "mo", "mr", "ms", "my", "nb", "ne", "nl", "no", "or", "pa", "pl", "pt", "ro", "ru", "si", "sk", "sl", "sq", "sr", "sv", "sw", "ta", "te", "th", "tl", "tr", "uk", "ur", "uz", "vi", "zh", "zh-rTW", "zu"
        //this removes regional variants from the support libraries
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
                arg("room.incremental", "true")
            }
        }
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
//            applicationIdSuffix ".debug"
        }
    }
    lintOptions {
        // comma separated list
        disable 'UseRequireInsteadOfGet'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.3.1"
//    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.preference:preference:1.1.1"
    implementation "androidx.media:media:1.4.1"
    implementation "androidx.palette:palette:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    kapt "androidx.room:room-compiler:2.3.0"
    implementation "androidx.room:room-runtime:2.3.0"
    implementation "com.android.billingclient:billing:4.0.0"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1"
    // viewpager2 doesnt respond to left/right press on TVs, don't migrate

    implementation "com.google.android.material:material:1.4.0"
    implementation 'com.jakewharton.timber:timber:5.0.0'
    implementation platform('com.google.firebase:firebase-bom:28.1.0')
    // Declare the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics'

    implementation "com.squareup.picasso:picasso:2.8"
    implementation "com.squareup.okhttp3:okhttp:3.14.9" // for picasso on R
    implementation "com.robinhood.spark:spark:1.2.0"
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    implementation 'com.github.anrwatchdog:anrwatchdog:1.4.0'
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'

    implementation project(':lastfm')

    testImplementation 'junit:junit:4.13.2'
    // androidTestImplementation 'androidx.test:runner:1.1.0-alpha1'
    // androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha1'
}
