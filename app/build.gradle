apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: 'keys.gradle'

android {

    def versionFile = file('version.txt')
    def verCode = 1
    if (versionFile.canRead()) {
        def line = (new FileInputStream(versionFile)).readLines()[0]
        verCode = line.toInteger()
        List<String> runTasks = gradle.startParameter.getTaskNames()
        for (String item : runTasks)
            if ( item.contains("assembleRelease") || item.contains("publishReleaseApk") || item.contains("bundleRelease")) {
                verCode++
                versionFile.text = verCode.toString()
            }
    } else {
        throw new GradleException("Could not read version.txt!")
    }

    compileSdkVersion 29
    defaultConfig {
        applicationId "com.arn.scrobble"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode verCode
        versionName ((int)(versionCode/100) + "." + (versionCode%100) +  " - " + new Date().format('dd.MMM.YY'))
        setProperty("archivesBaseName", "pScrobbler")
        vectorDrawables.useSupportLibrary = true
        resConfig "en"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
        }
    }
}

targetCompatibility = "1.8"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.preference:preference:1.1.0"
    implementation "androidx.media:media:1.1.0"
    implementation "androidx.palette:palette:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
//    implementation "android.arch.lifecycle:common-java8:1.1.1"
    kapt "androidx.lifecycle:lifecycle-compiler:2.2.0"
    kapt "androidx.room:room-compiler:2.2.3"
    implementation "androidx.room:room-runtime:2.2.3"

    implementation "com.google.android.material:material:1.1.0-beta01"

    implementation "com.squareup.picasso:picasso:2.71828"
    implementation "de.hdodenhof:circleimageview:3.1.0"
    implementation "com.robinhood.spark:spark:1.2.0"
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.1'

    implementation project(':lastfm')

    testImplementation 'junit:junit:4.12'
    // androidTestImplementation 'androidx.test:runner:1.1.0-alpha1'
    // androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha1'
}
